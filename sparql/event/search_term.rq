PREFIX label: <http://purl.org/net/vocab/2004/03/label#>
PREFIX ontology: <http://dbpedia.org/ontology/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX crm:  <http://www.cidoc-crm.org/cidoc-crm/>
PREFIX rdf:  <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX veniss_ontology: <https://veniss.net/ontology#>
PREFIX veniss_types: <https://veniss.net/resource/type/>

INSERT {
  ?item crm:P1_is_identified_by ?st .
  ?st a crm:E41_Appellation ;
      crm:P2_has_type veniss_types:search_term ;
      rdfs:label ?label .
}
WHERE {
  # --- only Events that DON'T already have a search_term ---
  {
    SELECT ?item WHERE {
      ?item rdf:type veniss_ontology:Event .
      FILTER NOT EXISTS {
        ?item crm:P1_is_identified_by ?existingSt .
        ?existingSt a crm:E41_Appellation ;
                    crm:P2_has_type veniss_types:search_term .
      }
    }
    # LIMIT 100
  }

  OPTIONAL {
    SELECT ?item ?lang (STR(?el) AS ?event_label_lang)
    WHERE {
      VALUES (?lang) { ("it") ("en") ("") }
      ?item rdfs:label ?el .
      FILTER( LANG(?el) = ?lang )
    }
  }
  
  OPTIONAL {
    SELECT ?item ?lang (STR(?tl) AS ?typology_text)
    WHERE {
      VALUES (?lang) { ("it") ("en") ("") }
      ?item crm:P2_has_type ?type_node .
      ?type_node crm:P71i_is_listed_in <https://veniss.net/resource/vocab/event_types> ;
                 rdfs:label ?tl .
      FILTER(LANG(?tl) = ?lang)
    }
  }
  
  OPTIONAL {
    SELECT ?item (SAMPLE(STR(?il)) AS ?island_text)
    WHERE {
      ?item veniss_ontology:has_island ?islandRes .
      ?islandRes crm:P1_is_identified_by ?islandId .
      ?islandId a crm:E41_Appellation ;
                crm:P2_has_type veniss_types:appellation ;
                rdfs:label ?il .
    }
    GROUP BY ?item
  }
  
  OPTIONAL {
    SELECT ?item (SAMPLE(STR(?dl)) AS ?date_text)
    WHERE {
      ?item crm:P4_has_time-span ?timespan .
      ?timespan crm:P170i_time_is_defined_by ?time_primitive .
      ?time_primitive a crm:E61_Time-Primitive ;
                      crm:P2_has_type <https://veniss.net/resource/type/time_primitive_year> ;
                      rdfs:label ?dl .
    }
    GROUP BY ?item
  }
  
  # ---- compose final label per (?item, ?lang) ----
  BIND(CONCAT(
        COALESCE(?event_label_lang, ""),
        IF(BOUND(?island_text),   CONCAT(" ", ?island_text),   ""),
        IF(BOUND(?typology_text), CONCAT(" ", ?typology_text), ""),
        IF(BOUND(?date_text),     CONCAT(" ", ?date_text),     "")
      ) AS ?labelStr)
  
    BIND(STRLANG(?labelStr, ?lang) AS ?label)
  
    BIND( IRI(CONCAT(STR(?item), "/search_term/", STRUUID())) AS ?st )
}